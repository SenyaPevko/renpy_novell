# Определение персонажей игры.
define m =  Character('Михаил Дмитриевич', color="#FEECB6",who_outlines=[ (3, "#592116") ], what_outlines=[ (2, "#67434d") ], what_color="#e3b9c6")
define p = Character('Паша', callback = name_callback, cb_name = "pavel", color="#FEECB6",who_outlines=[ (3, "#592116") ], what_outlines=[ (2, "#67434d") ], what_color="#e3b9c6")
define u = Character('Юля', callback = name_callback, cb_name = "julia", color="#FEECB6",who_outlines=[ (3, "#592116") ], what_outlines=[ (2, "#67434d") ], what_color="#e3b9c6")
define s = Character('Стёпа', callback = name_callback, cb_name = "stepan", color="#FEECB6",who_outlines=[ (3, "#592116") ], what_outlines=[ (2, "#67434d") ], what_color="#e3b9c6")
define al = Character('Алекс', callback = name_callback, cb_name = "alex", color="#FEECB6",who_outlines=[ (3, "#592116") ], what_outlines=[ (2, "#67434d") ], what_color="#e3b9c6")

image stepan ordinary highlight = At("stepan ordinary", sprite_highlight("stepan"))
image stepan angry highlight = At("stepan angry", sprite_highlight("stepan"))
image pavel ordinary highlight = At("pavel ordinary", sprite_highlight("pavel"))
image pavel angry highlight = At("pavel angry", sprite_highlight("pavel"))
image julia ordinary highlight = At("julia ordinary", sprite_highlight("julia"))
image julia angry highlight = At("julia angry", sprite_highlight("julia"))
image alex ordinary highlight = At("alex ordinary", sprite_highlight("alex"))
image alex angry highlight = At("alex angry", sprite_highlight("alex"))

# Звуки и музыка
define audio.wholegame = "audio/music/wholegame.mp3"
define audio.alarm = "audio/sounds/alarm.mp3"
define audio.coffee = "audio/sounds/coffee.wav"
define audio.phone_call = "audio/sounds/phone_call.wav"
define audio.sneeze = "audio/sounds/sneeze.mp3"
define audio.laugh = "audio/sounds/laugh.wav"
define audio.dropped_call = "audio/sounds/dropped_call.wav"
define audio.dropped_glass = "audio/sounds/dropped_glass.wav"

#загрузка игры
label splashscreen:
    $ renpy.movie_cutscene('video/intro.ogv')
    return

# Игра начинается здесь:
label start:

    $ player_name = ""
    
    #выбор имени и цвета
    jump choose_color

    #создание главного персонажа
    label define_main_player:

        #проверка имени на корректность
        python:
            if player_name == "": 
                player_name = "Алексей"
            else:
                player_name = player_name[0].upper() + player_name[1:]
            
            #изменяем текст автора
            style.say_thought.color = cs.set_color()[0]
            style.say_thought.outlines = [(2, cs.set_color()[1], 0, 0)]
            style.rebuild()

        $ player_color = cs.set_color()[0]
        $ player_outcolor = cs.set_color()[1]

        $ a =  Character("[player_name]", color="#FEECB6",who_outlines=[ (3, "#592116") ], what_outlines=[ (2, player_outcolor) ], what_color= player_color)

        $ narrator = Character(callback = name_callback, cb_name = None, what_outlines=[ (2, player_outcolor) ], what_color= player_color)
        


    play music wholegame fadeout 2.0 fadein 2.0

    #начало первой главы
    $ persistent.ending1 = False
    
    scene bg radio
    with fade

    "Я давно хотел работать с техникой, еще с малых лет."

    "Помню дед при мне пытался чинить свое старенькое радио советского производства. А я помогал. Ну как помогал, пытался светить в нужное место динамо фонарем, то и дело роняя его из-за усталости кисти."

    "Помимо ругани в его словах мелькали интересные подробности работы сие дивного устройства. А я слушал и запоминал, мечтал тоже встать когда-то у какого-нибудь навороченного аппарата и нажимать светящиеся кнопки с умным видом и крестовой отверткой за пазухой."

    scene bg computer off
    with fade

    "В какой-то степени моя мечта осуществилась. После горы переломанных бытовых приборов и множества часов стояния в углу, родители сжалились и купили мне компьютер, взяв с меня обещание, что я его точно не разберу по винтикам к концу недели."
    
    scene bg buying a disc
    with fade

    "Вообще они купили мне его для учебы. Но об учебе я в тот момент не думал."
    
    scene bg computer loading
    with fade

    "Первым делом я разбил свою маленькую копилку, соскреб все свои пожитки в кучу и пошел в магазин дисков."

    scene bg computer heroes three
    with fade

    "Счастья моему не было придела, когда я тайком от сидящего на диване отца и готовящей ужин на кухне матери пронес под курткой свой первый диск с Третьими героями."

    scene bg thinking
    with fade

    "Вдоволь наигравшись, я в силу своего любознательного характера начал задумываться: если компьютер работает из-за микросхем и транзисторов и выполняет четко указанную цель, как могут так волшебно двигаться эти картинки на экране?"
    
    "Ведь не проводов, ни лампочек в диске не было. Так началась моя вторая глава изучения компьютера – неосязаемая; понять как работает все внутри, а главное – почему."
    
    window hide

    # scene bg chapter1
    # with fade
    
    #конец первой главы
    $ persistent.ending1 = True

    play sound alarm fadeout 1.0 fadein 1.0 volume 0.6

    scene bg transition
    with fade

    pause

    stop sound fadeout 0.5

    #начало второй главы
    $ persistent.ending2 = False

    scene bg alarm ringing
    with fade 

    "Вот и долгожданные выходные. Пусть я и работаю уже год на дистанционке, все равно жду выходных, как день рождения. Может быть, привычка."

    scene bg alarm
    with fade

    "За три года работы старшим программистом в мелкой, но подающей успехи российской IT компании, когда что не срок сдачи, то дедлайн, невольно начинаешь ценить сон и нервы."
     
    "Хотя и в универе такое было, но здесь куда больше ответственности, тем более когда все лежит на своих плечах в отделе."
    
    play sound coffee fadeout 1.0 fadein 1.0
    
    "Ну-ка, что там нового пишут в Интернете"
    
    "Я начал было заниматься своим неимоверно банально досугом: поскроллить ленту, посмотреть почту, поскроллить ленту, глянуть смешной видос с котами, поскроллить ленту."
    
    "Но в один момент кое – что произошло. Кое-что неожиданное. Кое-что, чему меня точно не обучали."
    
    play sound phone_call fadeout 1.0 fadein 1.0
    
    scene bg phone conversation
    with fade

    a "Алло?"

    m "Алло? [player_name], ты срочно должен быть в офисе через полтора часа."

    a "Но Михаил Дмитриевич, выходной же. К тому же, что за спешка? Сервера не упали, а сгорели что-ли?"

    m "Очень смешно. Даже наоборот, новости просто отличные, совет директоров решил расширяться. Но не по-крупному конечно, филиалов новых не будет…"

    a "Аааааа…"

    m "и зарплату не повысим…"

    "Черт."

    m "Пока что… Ну, как говорится работа не волк, все равно в лес смотрит. Или как там…"

    "Хоть и моему начальнику и было всего сорок пять, но иногда он напоминал какого – то деда с этими его крылатыми фразочками."

    m "…Однако они решили расширить штаб и сегодня будет «поступление» новых кадров, и ты должен их принять и провести брифинг."

    a "А почему этим занимается не положенный для этого человек?"

    m "Лена в декрете…"

    menu:
        "Попытаться отказать":
            a "А почему я?"

            m "Потому что ты единственный, до кого я смог дозвониться в восемь утра в выходной день."

            "М-да, веская причина."

        "Согласиться":
            a "Принимать новичков прямо с утра, жертвуя выходным – отличное начало дня! Ага, ну и бредятина."

            m "Вот это настрой! Я думаю ты точно справишься."

    a "Ладно, понял. А кто эти «зеленые»? Резюме есть?"

    m "Я тебе уже выслал на почту. Не на ту, где ты собираешь промо акции для своих бутиков с одеждой, а на рабочую. Ее хотя бы проверяй иногда на уик-эндах."

    a "Эх, хорошо Михаил Дмитриевич, уже пошел собираться."

    m "Леша, не переживай, они же не кусаются. Зубки то еще молочные. Аха-ха-ха."

    scene bg phone
    with fade

    "На той стороне послышались гудки"

    scene bg alarm continues
    with fade

    "Меня обучали трем языкам, я изучал среды разработки, господи, мне даже пришлось изучать высшую математику, но работать с людьми мне особо не приходилось. По своей натуре я был… сычом. Даже поручения тимлида я получал по почте, даже во время очной работы…"

    "Но я любил свою работу, и новость о расширении меня искренне порадовала. И тем более, я что, не смогу объяснить то, что знаю как свои пять пальцев?"

    "Я уже собрался уходить, но вспомнил, что лучше бы все же проверить свою почту на наличие резюме и допить ещё тёплый кофе, чтобы не уснуть перед важным моментом - это я могу."

    menu:
        "ПОСПЕШИТЬ НА ВСТРЕЧУ":
            jump hurry_up

        "ЗАДЕРЖАТЬСЯ":
            jump stay_in

    label hurry_up:

        a "Ладно, на месте разберемся, лишь бы их не перепутать."

        scene bg bus_stop
        with fade

        "Я вышел на улицу, и прохладный воздух ударил меня в нос. Я и забыл каково это, выходить на улицу октябрьским утром. Такой холод должен развеять остатки сна, иначе я проеду лишнюю остановку."

        "Всю дорогу меня никак не могло покинуть чувство дежавю. Где-то я уже это видел… А, точно. Когда меня самого принимали на работу. Я точно также волновался: как себе вести на собеседовании? Что говорить? Как не ударить лицом в грязь?" 
        
        "Сейчас происходит то же самое, только зеркально."

        scene bg office hall
        with fade
        
        "Я прибыл на место, зашел в совершенно пустой офис (если не считать уборщицу тетю Зину, которая, как мне кажется, живет прям здесь), зашел в переговорную и принялся ждать."

        scene bg meeting room
        with fade

        "…"

        "Меня разбудил звонок. Это был Михаил Дмитриевич."

        m "Алло? [player_name], ты на месте уже?"

        "Я немного задремал, к счастью, ненадолго."

        a "Аа.. да.. да! Я уже тут."

        m "Тебе нужно будет их принять разом, потому что они будут работать на одним проектом. Как раз познакомятся."

        "С одной стороны, так все быстрее закончится, а с другой, общение сразу с несколькими людьми…Я бы лучше подольше посидел."

        a "Михаил Дмитриевич, но они же не дети в конце концов."

        m "Ошибаешься, Паша, они еще пороху не нюхавшие, жизнь не видевшие, что с них взять."

        "Также он выделялся особенным скептицизмом к людям младше его. В том числе и ко мне."

        a "Ладно, ладно, понял я, сейчас пойду встречу"

        m "Отлично. Ни пуха, ни шеста! Или как там говорится?.."

        "Я сбросил трубку. Слышать вот такие вот пируэты уже немного утомило. Ладно вернемся к нашим баранам…"

        "Кажется, я потихоньку превращаюсь в своего начальника."

        "…"

        show pavel ordinary:
            xalign 0
            yalign 0
        with easeinleft

        show julia ordinary:
            xalign 0.3
            yalign 0
        with easeinleft

        show alex ordinary:
            xalign 0.6
            yalign 0
        with easeinright

        show stepan ordinary:
            xalign 0.9
            yalign 0
        with easeinright

        "Итак, я позвал «гостей» в переговорку. В комнату вошло четыре человека. Милая девушка лет двадцати, высокий и худощавый парнишка в очках в своеобразном свитере, сбитый молодой человек с волевым подбородком и... "
        
        "на изумление элегантно одетый в строгий деловой костюм джентльмен. В его до безобразия блестящих волосах отражалось солнце и слепило остальных ребят."

        "Ну, начнем – подумал я про себя"

        jump second_chapter_end
    
    label stay_in:

        a "Та-а-к, и что тут у нас… Ага…"

        "Я, стоя на одной разутой ноге, мельком взглянул на досье, постарался хотя бы запомнить имена."

        "Я вышел на улицу, и прохладный воздух ударил меня в нос. Я и забыл каково это, выходить на улицу октябрьским утром. Утром… блин, автобус!..."

        "Черт, я пропустил единственный автобус, идущий прямо до офиса. На метро путь будет раза в два дольше. Я как всегда. Зато будет время продумать все в голове."

        scene bg city
        with fade

        "Кофе прогнал остаток сна. Я взглянул на запястье и ужаснулся: я опоздал на десять минут. И все из-за моей нерасторопности. Хотя Мишка сам виноват, знал же кому звонит."

        "Как только я полез в карман за телефоном, тут же раздался звонок."

        m "Алло? [player_name], ты на месте уже? Они уже пришли и ждут только тебя! Где тебя черти носят?"

        a "Простите, Михаил Дмитриевич, автобус задержался, и я…"

        m "Да неважно уже! Руки в ноги и побежал кабанчиком! Не трать их время, это очень ценные кадры!"

        "Я побежал трусцой, то и дело поправляя сумку на плече. И когда начальник стал серьезным? Обычно он повышал голос, когда смеялся с собственной шутки…"

        a "Я уже подхожу, вот-вот войду в здание."

        scene bg office hall
        with fade

        m "Тебе нужно будет их принять разом, потому что они будут работать на одним проектом. Как раз познакомятся."

        "С одной стороны, так все быстрее закончится, а с другой, общение сразу с несколькими людьми…Я бы лучше подольше посидел."

        a "Михаил Дмитриевич, но они же не дети в конце концов"

        m "Ошибаешься, Паша, они еще пороху не нюхавшие, жизнь не видевшие, что с них взять."

        "Также он выделялся особенным скептицизмом к людям младше его. В том числе и ко мне."

        a "Ладно, ладно, понял я, сейчас пойду встречу."

        m "Отлично. Ни пуха, ни шеста! Или как там говорится?.."

        "Я сбросил трубку. Слышать вот такие вот пируэты уже немного утомило. Ладно вернемся к нашим баранам…"

        "Кажется, я потихоньку превращаюсь в своего начальника."

        "…"

        scene bg waiting room
        with fade
        
        "В комнате ожидания уже сидели виновники торжества. По их лицам было понятно, что задержался я довольно значительно. Я стал вспоминать лица и имена из резюме."

        show pavel angry:
            xalign 0
            yalign 0
        with easeinleft

        show julia angry:
            xalign 0.3
            yalign 0
        with easeinleft

        show alex angry:
            xalign 0.6
            yalign 0
        with easeinright

        show stepan angry:
            xalign 0.9
            yalign 0
        with easeinright

        "Милая девушка лет двадцати, высокий и худощавый парнишка в очках в своеобразном свитере, сбитый молодой человек с волевым подбородком и на изумление элегантно одетый в строгий деловой костюм джентльмен." 
        
        "В его до безобразия блестящих волосах отражалось солнце и слепило остальных ребят."

        "Ну, начнем – подумал я про себя"
        
        jump second_chapter_end

    label second_chapter_end:
        #конец второй главы
        $ persistent.ending2 = True
    
    #начало третьей главы
    $ persistent.ending3 = False

    scene bg meeting room
    with fade

    show pavel ordinary:
        xalign 0
        yalign 0
    with easeinleft

    show julia ordinary:
        xalign 0.3
        yalign 0
    with easeinleft

    show alex ordinary:
        xalign 0.6
        yalign 0
    with easeinright

    show stepan ordinary:
        xalign 0.9
        yalign 0
    with easeinright
    
    "С виду они казались организованной группой, будто они учились в одном универе. Однако это было не так. Я чувствовал себя учителем, встречающим первоклашек на день знаний. Учителем, который сам только-только устроился на работу."

    "Они молча зашли в переговорку, сели на кресла и молча уставились на меня. Мне стало не по себе. Ребята эти явно что-то знают…"

    a "Итак, давайте знакомиться. Меня зовут [player_name] Владимирович Резнов, сегодня я проведу некий «брифинг» и расскажу вам, что вам делать, вернее, как делать и зачем делать. Начну спрашивать по порядку, но, если есть что сказать, говорите, спрашивайте. У кого-то есть вопросы?"

    "Так же начинают собеседование?"

    hide julia ordinary
    hide alex ordinary
    hide stepan ordinary

    p "У м-меня есть…."

    "Слова эти сказаны были довольно тихо, и, каюсь, я не обратил на них внимание."

    hide pavel ordinary

    a "Отлично, приступим."

    "Сладостная и нагнетающая тишина провисела еще секунду."

    show julia happy highlight:
        xalign 0.5
    with dissolve

    u "Ну тогда начну я! – звонким голоском отозвалось где-то с краю."

    "Это была та девушка. Внешность была вполне… молодежной? Боже, какой я старый. Крашеные волосы, линзы в глазах, больше чем очки другого паренька, пирсинг в ушах и дизайнерская толстовка. Сквозь линзы виднелся огонек в глазах, а ненавязчивая улыбка, кажется, растопила лёд между всеми в аудитории."

    show julia ordinary highlight

    u "Меня зовут Юлиана, можно просто Юля! Я получила медаль в школе в Волгограде и там же поступила в местный вуз, хорошо отучилась и получила красный диплом… Та-а-ак…Ходила на всякие кружки, мама записывала: рисование, танцы, репетиторы. Вроде три репетитора было."

    u "Ну я особо не жаловалась, мама же сказала, что надо ходить, чтобы стать успешной и богатой, я и ходила. Я пыталась все успеть, и вроде как успевала."

    show julia thinking highlight

    "Она на секунду опустила взгляд и задумалась, будто прогоняя все сказанное в голове."

    show julia happy highlight

    u "А, еще я умею играть на губной гармошке, сейчас достану, где же она?.."

    a "Не стоит, правд.. – начал было я, но…"

    #show julia_with_harmonica

    "От Юли прямо веяло, нет, вернее, несло энтузиазмом и оптимизмом. Это будет хорошо влиять на команду. Только вот она какая – то гиперактивная. Наверное, чтобы так не думать, надо самому почаще на улицу выходить дальше продуктового."

    show julia ordinary highlight

    a "Ладно-ладно, хватит, мы поняли. Ох.. Так кем вы себя представляете?"

    u "На работе? Делать то, что мне скажут, мне в целом не важно. Я хорошо выполняю любые задания."

    "Видно эта привычка осталась со школы, когда ради оценки ты учил стих на перемене, чтобы вскоре его забыть. Ради оценки, а не знаний…"

    a "Усложним вопрос. Кем вы себя видите в жизни?"

    show julia thinking highlight
    
    u "Ой… Я.. Я даже не знаю, что ответить…"

    "Надеюсь, я не зря эту тему поднял, не хотелось девочке настроение испортить, тем более, блин, это собеседование, а не сеанс у психолога. Надо было раньше думать."

    "Нависла тишина. Снова."

    show julia ordinary highlight

    u "Я еще с пятого класса хотела творить что-то сама, придумывать, рисовать, писать, создавать в конце концов. Самой. Самой решать, что делать, но как-то не получалось."

    u "В школе уповала на новую жизнь, мол, я стану свободной от всех этих оков кружков, и надоевших репетиторов. В универе я надеялась на жизнь после его окончания, когда наконец перестану сдавать эти лабораторные и конспекты. Не знаю, стоит ли надеяться на что-то в третий раз…"

    "Кажется, этот вопрос заставил её нехило задуматься. В нынешнее время ты заранее знаешь, какой ответ хочет услышать вопрошающий, и говоришь то, что надо, а не что думаешь сам. Как же это знакомо. Именно так убивают в себе творчество и желание думать самому."

    show julia happy highlight

    u "Но я все же оптимистка, поэтому поживем-увидим."

    a "А что вы скажите по поводу своих умений?"

    show julia ordinary highlight

    u "Когда у меня было свободное время, а это часа три-четыре в неделю, я занималась 3D-моделированием. Вроде получалось, но я никому особо не показывала."

    "Я мельком глянул в резюме.  Я ожидал увидеть простые бублики или несложные геометрические модели, но не как не полностью обработанные в блендере ростовые человеческие скульптуры, замудрёные сцены городских и природных пейзажей. Это было действительно впечатляюще."

    "Я сделал вид, что записал, все что она сказала. На самом деле я записал только имя, остальное я и так хорошо запомнил."

    a "Та-а-а-к, хорошо. Юлиана, верно? Угу. Ну, кто следующий?"

    hide julia ordinary highlight
    with dissolve

    show stepan ordinary highlight
    with dissolve

    s "Ну давай я. Ёмаё, в смысле {i}«давайте»{/i}."

    "Это сказал коренастый парень. Короткие волосы, волевое, непробиваемое взглядом грубое, но доброе и симметричное лицо. Одет он был в кожаную куртку, на шее была какая – то татуировка. Его внешний вид внушал одновременно некое беспокойство, но в то же время и доверие."

    s "Меня кличут Стёпа. Честно скажу, я фиг знает, как я тут вообще оказался. Тут вроде состоятельные люди сидят, я тут что забыл?"

    a "В каком смысле?"

    show stepan thinking highlight
    with dissolve

    s "Ну не в плохом. Я как бы.. ну не имею высшего образования."

    "Интересно. Если он получил приглашение, не имея высшего образование, значит его позвали за другие заслуги…"

    show stepan ordinary highlight

    s "Ну это… я в школе у себя в мелком городке так-то плохо учился, драки там, гулянки. Но бате все равно было. Он считал, что оценки и остальное – это всё «конкретная такая лабуда», понял, да? За оценки он не возникал, не бил меня."

    s "Отроком был, хвостиком за ним бегал, даже школу прогуливал чтобы со стариком своим побыть. А работал он в ремонтном центре, там болтики-винтики крутил. А я палил и на корочку записывал. Потом кое-как девятый закончил значит, поступил в ПТУ и с первого курса начал там «работать»."

    s "Преподы постоянно просили меня их компы чинить, потому что они старые, рамсили часто. Не преподы, компы, да…"
    show stepan happy highlight
    
    s "Автоматы ставили. Главное, чтобы буквальный «автомат» не получить, хе-хе."

    show stepan ordinary highlight

    s "Ну во-о-т. А дальше с 3 курса я пошел работать в местный магаз электроники. Там у них сайтик с данными был дряхлый. Я помимо работы в зале, одним глазком за сервером смотрел, чтоб не сгорел нафиг… Да-а-а, техника была хреновей некуда…"

    s "А потом мне мой бывший препод смс уронил, сказал что по его наводке меня сюда позвали. А я может и дурак, но не идиот, за возможность взялся. Может он твоег… вашего начальника знает. Ну вот, короче, я здесь."

    "Я понял. Его позвали не столько за теорию, сколько за практику. С раннего детства вырос среди техники, дальнейшая работа также с этой самой техникой. И уверенность в себе как никак. Пусть манера речи и оставляет пятно на впечатлении, но общая картина полностью перекрывает все недостатки."

    "Хм, познакомился с компьютерами в детстве, знакомо. Только если я пошел в область программирования, он решил свернуть немного в другую сторону. Оно и лучше для команды, теория теорией, но без практических навыков от нее пользы будет немного…"

    a "Очень интересно, Степан…"

    show stepan happy highlight

    "Просто Стёпа, будьте добры."

    "Он улыбнулся и почесал затылок. Было видно, пусть он и крепкий характером на вид, но все равно немного волнуется."

    hide stepan happy highlight

    "В аудитории снова стало тихо, пока я записывал имя. Но опять ненадолго. Тишину разорвало высказывание следующего гостя."

    show alex ordinary highlight
    with dissolve

    al "Это конечно мило, но можно уже я начну?"

    "Прозвучали эти слова с ноткой наглости и надменности. Это подал голос тот смазливый молодой человек."

    al "Вообще, честно признаюсь, я удивлен, что начали не с меня."

    a "И почему же?"

    "Все презрительно на него посмотрели. Особенно Стёпа, видно таких выскочек он {i}особо{/i} любил."

    al "Да потому что, как я понял, у меня тут лучше образование. Да и сам я, как понял лучше"

    show alex ordinary highlight:
        xalign 0.8
    with move
    
    show stepan angry highlight:
        xalign 0.2
    with easeinleft
    s "Ну тогда назовись, че ты тянешь тогда, золотой ты наш?"

    show stepan angry highlight:
        xalign -0.2
    with move
    hide stepan angry

    show alex ordinary highlight:
        xalign 0.5
    with move

    a "Меня зовут Алекс, я учился в московском лицее, легко получил медаль, отучился в МГУ, забрал положенный мне диплом. Затем, как ожидалось, мне поступил звонок с приглашением сюда."
    
    show alex contemptuous highlight

    al "Честно не знаю, затем я пришёл в это… заведение. Наверное, просто из любопытства, посмотреть, как простой люд живёт. Благо у меня в эту глушь добраться много времени не заняло на своей то машинке."

    show alex contemptuous highlight:
        xalign 0.8
    with move
    
    show stepan angry highlight:
        xalign 0.2
    with easeinleft

    s "Слышь, Шура, ты так бы особо не возникал, да?"

    al "А ты мне что сделаешь, деревня?"

    s "Да я тебя пополам…"

    a "Так, хватит, не надо тут детский сад разводить, вы же все взрослые люди, ей богу"

    show alex ordinary highlight

    show stepan ordinary highlight

    "М-да, несмотря на хороший внешний вид, ведёт он себя как абсолютный невежа. Ещё и срывает наше и так непростое собеседование."


    show stepan ordinary highlight:
        xalign -0.2
    with move
    hide stepan ordinary highlight

    a "Эй, как тебя, Юля, не хочешь сегодня покататься?"

    show alex ordinary highlight:
        xalign 0.8
    with move

    show julia ordinary highlight:
        xalign 0.2
    with easeinleft

    u "Воздержусь, пожалуй."

    show julia ordinary highlight:
        xalign -0.2
    with move
    hide julia ordinary highlight

    a "Александр, по делу, давайте закончим."

    al "Алекс вообще-то."

    show alex ordinary highlight:
        xalign 0.8
    with move
    
    show stepan ordinary highlight:
        xalign 0.2
    with easeinleft

    s "Да не важно."

    show stepan ordinary highlight:
        xalign -0.2
    with move
    hide stepan ordinary highlight

    show alex ordinary highlight:
        xalign 0.5
    with move

    a "Ладно, Алекс, лучше расскажите о ваших навыках и качествах."

    al "Ну, разумеется, я отличный программист, можно было и догадаться. Я в лицее всем в электронном дневнике ставил одни пятёрки. Разумеется, тем кто этого заслужил."

    al "Затем я сам, без помощи, написал скрипт, который при попытке запуске компьютера, заставлял его переходить в бесконечный спам консолью и смешным звуками. И это в седьмом классе, между прочим."

    al "Потом я начал сам зарабатывать, создавая кейлоггеры и занимаясь фишингом. Да если я захочу остаться, вам без меня никуда. Там в резюме есть все данные."

    "Пусть он и невозможен, его портфолио впечатляет. Может он самовольно откажется. В теории так и так будет хорошо, по понятным причинам."

    a "Так, Алекс, все записал. Вроде бы всех послу…"

    show alex ordinary highlight:
        xalign -0.2
    with move
    hide alex ordinary highlight

    show pavel ordinary highlight:
        xalign 0.8
    with easeinright

    p "И-извините, а я?"

    "О, господи, я про него и забыл совсем. Этот паренёк сел в самом углу, и я его не сразу заметил."

    "Действительно. Очень он был не вызывающе, а весьма скромно. Скромный свитер, скромный штаны, да и сам он скромный. Уселся с краю, признаком жизни до этого момента не подавал. Только очки время от времени сверкали из угла."

    a "Простите, виноват. Итак, как вас зовут? Где учились? Что умеете?"

    p "П-павел. Я у-учился на дому, потому что я оч-чень болезненный, даже сейчас я… я…."

    show pavel covered face highlight

    play sound sneeze fadeout 1.0 fadein 1.0

    show julia ordinary highlight:
        xalign 0.2
    with easeinleft

    u "Будь здоров."

    show julia ordinary highlight:
        xalign -0.2
    with move
    hide julia ordinary highlight

    show stepan ordinary highlight:
        xalign 0.2
    with easeinleft

    s "Расти большой."

    show stepan ordinary highlight:
        xalign -0.2
    with move
    hide stepan ordinary highlight

    show alex ordinary highlight:
        xalign 0.2
    with easeinleft

    al "Не делай так больше."

    show alex ordinary highlight:
        xalign -0.2
    with move
    hide alex ordinary highlight

    show pavel ordinary highlight:
        xalign 0.5
    with move

    p "…немного приболел."

    a "Почему вы сразу не сказали? Можно было бы перенести на потом встречу?"

    p "Я не привык кому-то мешать, з-знаете. Я не люблю надоедать людям. Во-от."

    a "Ну расскажите тогда, что практикуете."

    p "Так как я большую часть учебы болел, меня перевели на домашнее обучение. И когда так много времени и нет желания выходить на улицу я стал увлекаться программированием. Меня мама записала на курс, чтобы я не скучал, но уже через 2 недели я его закончил, и опять мне становилось скучно."

    p "И я решил написать что-то своё. Начал я с малого, писал ботов для соцсетей, потом начал изучать другие языки и так по накатанной. Делал патчи для старых игр, чтобы они нормально запускались на новых системах, делал моды, исправляющие баги в играх и так далее."

    "Хм, из него бы получился хороший бек-энд разработчик, если он исправлял баги и ошибки в старых играх, где даже умелый программист сломает ногу в богом забытом и непонятном коде. Эх, поиграть бы сейчас в третьих героев…"

    a "А что вы думаете по поводу работы в коллективе?"

    show pavel thinking highlight

    p "Н-ну я особо кроме мамы ни с кем не общался, я и одноклассников то толком не помню если честно. А, еще я на дваче там сидел бывало."

    show pavel screwed up highlight:
        xalign 0.8
    with move

    show stepan happy highlight:
        xalign 0.7
    with easeinright

    hide pavel screwed up highlight
    show pavel screwed up highlight:
        xalign 0.8

    s "Не парься, Пахан, все нормально будет!"

    "Стёпа по-братски хлопнул Пашу по плечу так, что тот чуть не сложился пополам."

    show stepan ordinary highlight

    s "Блин, прости, не больно?"

    p "Н-нормально."

    play sound laugh fadeout 1.0 fadein 1.0

    show alex ordinary highlight:
        xalign 0.2
    with easeinleft

    al "Если ты такой нелюдимый, зачем сюда пришёл тогда? Так бы и сидел дома, на дваче бы жил да «ботиков» своих писал."

    show julia ordinary highlight:
        xalign 0.6
    with easeinright

    hide stepan happy highlight
    show stepan happy highlight:
        xalign 0.7

    hide pavel screwed up highlight
    show pavel screwed up highlight:
        xalign 0.8

    u "Отстань от него, он человек получше тебя будет, уж точно."

    s "Базар."

    p "М-может мне и вправду стоило дома остаться?"

    a "Нет, что вы? В наше время у всех есть свои… проблемы. Как я понял, вам лучше работать из дому?"

    show alex ordinary highlight:
        xalign -0.2
    with move
    hide alex ordinary

    show stepan happy highlight:
        xalign 1.2
    with move
    hide stepan happy highlight

    show julia ordinary highlight:
        xalign 1.2
    with move
    hide julia ordinary highlight

    show pavel ordinary highlight:
        xalign 0.5
    with move

    p "Н-на самом деле я давно понял, ч-чтобы стать лучше, нужно перешагнуть через себя. Возможность прийти на настоящее собеседование… Я не мог такое упустить. Возможно эти билет в новую жизнь, шанс выйти из зоны комфорта. Измениться в конце концов."

    "Этот парень чем-то напоминает меня. Я тоже особо не люблю громкие вечеринки, но до такого я пытался себя не запускать. Но если он готов пойти на такие жертвы, ради личностного роста, это довольно сильный поступок. Таким людям все рады."

    hide pavel ordinary highlight
    with dissolve

    a "Та-а-к.. ну вот, мы все познакомились."

    show julia happy highlight:
        xalign 0.2
    with easeinleft

    u "Я так рада, что мне удалось сегодня прийти, если честно. Я так устала дома сидеть, хоть с кем-то познакомилась"

    show stepan happy highlight:
        xalign 0.8
    with easeinright

    s "А то, глядишь сработаемся, и все чётко будет, да?"

    show alex ordinary highlight:
        xalign 0.3
    with easeinleft

    hide julia happy highlight
    show julia happy highlight:
        xalign 0.2

    al "Да что вы…"

    show stepan angry highlight

    s "Тебя это не касается, понял?"

    show pavel ordinary highlight:
        xalign 0.7
    with easeinright

    hide stepan angry highlight
    show stepan angry highlight:
        xalign 0.8

    p "Р-ребята, не надо."

    "Ничего, помирятся. Как никак труд сплочает…"

    "Ну насколько я знаю."

    show julia happy highlight:
        xalign -0.2
    with move
    hide julia happy highlight

    show stepan happy highlight:
        xalign 1.2
    with move
    hide stepan happy highlight

    show alex ordinary highlight:
        xalign -0.2
    with move
    hide alex ordinary highlight

    show pavel ordinary highlight:
        xalign 1.2
    with move
    hide pavel ordinary highlight

    a "Так, мне нужно выйти позвонить, особо не хулиганьте."

    show stepan ordinary highlight
    with dissolve
    
    s "Да не парься, начальник, всё ок будет."

    a "Я вам не начальник, я просто провожу экскурс."

    show julia happy highlight:
        xalign 0.2
    with easeinleft 

    u "Тогда уж наставник"

    "Она улыбнулась."

    show pavel ordinary highlight:
        xalign 0.7
    with easeinright

    p "Т-тренер получается."

    "Если Юля мило улыбнулась, то Стёпа громко засмеялся и уж было хотел снова ударить Пашу по плечу, но тот вовремя нагнулся."

    s "Слушай, а ты шустрый."

    p "С-спасибо, наверное…"

    show julia happy highlight:
        xalign -0.2
    with move
    hide julia happy highlight

    show pavel ordinary highlight:
        xalign 1.2
    with move
    hide pavel ordinary highlight

    hide stepan ordinary highlight
    with dissolve

    scene bg phone conversation

    "Я вышел из аудитории и набрал начальника."

    a "Алло? Алло?"

    m "Чего? Не слышу?"

    "У Миши по ту сторону трубки громко играла музыка. Значит на выходные кому-то идти на работу, а кто-то на дачу на шашлыки едет? Мир несправедлив."

    m "Что там у тебя?"

    a "Мы познакомились все, вроде как нормально все прошло, но там есть тип один."

    m "Ты про этого, как его, «Алехандро»? Не волнуйся, он сначала тоже мне не понравился, но в нем есть задатки хорошего фронт-разработчика. А это его поведение…Знаешь, это типа защитного механизма из-за нового окружение. Парнишка-то этот, хоть и избалованный малость, но в целом адекватный."

    "Слава Ктулху, что это мне не придется с ним работать."

    m "Ну ты это, знакомство-знакомством, а брифинг-то проведи."

    play sound dropped_glass fadeout 1.0 fadein 1.0

    m "ДЕНИС! ЕЛКИ-ПАЛКИ! НОВЫЙ ФУЖЕР КУПИЛ! Лена! Забери у него стакан ради бога! Мы ещё не закончили подготовку! Пусть лучше мангал притащит!"

    "{i}«Подготовку»?{/i}"

    a "А что вы там делаете вообще? И что там Денис забыл и Лена? Что вы мне там не договаривается?"

    m "Эээ, слушай, [player_name] , пш-пшп-пш, у меня, это, пш-пш, помехи, тут плохо сеть, пш-пш, ловит, пш-пш."

    scene bg phone

    play sound dropped_call fadeout 1.0 fadein 1.0

    "Веселятся там, а меня не позвали? Чем я провинился… Скинули мне этих зеленых, а сами пошли шашлыки готовить… А Лена так вообще должна в декрете дома сидеть!"

    "С этой грустной мыслью я зашел обратно."

    scene bg meeting room

    a "Итак, продолжим…"

    #конец третьей главы
    $ persistent.ending3= True

    return 


#выбор цвета
label choose_color:
    python:
        class SetColor(object):
            def __init__(self):
                self.red = 0
                self.green = 0
                self.blue = 0
            
            def set_color(self):
                return (Color('#%02x%02x%02x' % (self.red, self.green, self.blue)), Color('#%02x%02x%02x' % (self.red if self.red <120 else self.red - 120, self.green if self.green <120 else self.green - 120, self.blue if self.blue <120 else self.blue - 120)))
                
            def screen_loop(self):
                renpy.show_screen("choose_color_screen")
                while True:
                    result = ui.interact()
                    
                    if result[0] == "choose_color":
                        current_color = result[1]
                    
                    else: return
                    
        cs = SetColor()
        cs.screen_loop()